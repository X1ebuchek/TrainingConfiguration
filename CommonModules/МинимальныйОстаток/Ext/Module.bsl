Процедура ЗаполнитьТаблицуМинимальногоОстатка(ТабЗнач) Экспорт

	ТабЗнач.Очистить();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Продукты.МинимальноеКоличество КАК МинимальноеКоличество,
		|	Продукты.Ссылка КАК Продукт,
		|	Продукты.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ПОМЕСТИТЬ ВтПродукты
		|ИЗ
		|	Справочник.Продукты КАК Продукты
		|ГДЕ
		|	Продукты.ПометкаУдаления = ЛОЖЬ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИнгредиентыВНаличииОстатки.Номенклатура КАК Номенклатура,
		|	ИнгредиентыВНаличииОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВтОстатки
		|ИЗ
		|	РегистрНакопления.ИнгредиентыВНаличии.Остатки(
		|			,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					ВтПродукты.Продукт КАК Продукт
		|				ИЗ
		|					ВтПродукты КАК ВтПродукты)) КАК ИнгредиентыВНаличииОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыПродуктовОстатки.Продукт КАК Продукт,
		|	ЕСТЬNULL(ЗаказыПродуктовОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВтЗакупки
		|ИЗ
		|	РегистрНакопления.ЗаказыПродуктов.Остатки(
		|			,
		|			Продукт В
		|				(ВЫБРАТЬ
		|					ВтПродукты.Продукт КАК Продукт
		|				ИЗ
		|					ВтПродукты КАК ВтПродукты)) КАК ЗаказыПродуктовОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтПродукты.Продукт КАК Продукт,
		|	ВтПродукты.МинимальноеКоличество КАК МинимальноеКоличество,
		|	ЕСТЬNULL(ВтОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ВтЗакупки.КоличествоОстаток, 0) КАК Заказано,
		|	ВтПродукты.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	ВтПродукты КАК ВтПродукты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОстатки КАК ВтОстатки
		|		ПО ВтПродукты.Продукт = ВтОстатки.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЗакупки КАК ВтЗакупки
		|		ПО ВтПродукты.Продукт = ВтЗакупки.Продукт
		|ГДЕ
		|	ВтПродукты.МинимальноеКоличество > ЕСТЬNULL(ВтОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(ВтЗакупки.КоличествоОстаток, 0)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтрокаТаблицы = ТабЗнач.Добавить();
		СтрокаТаблицы.Продукт = ВыборкаДетальныеЗаписи.Продукт;
		СтрокаТаблицы.МинимальноеКоличество = ВыборкаДетальныеЗаписи.МинимальноеКоличество;
		СтрокаТаблицы.КоличествоОстаток = ВыборкаДетальныеЗаписи.КоличествоОстаток;
		СтрокаТаблицы.Заказано = ВыборкаДетальныеЗаписи.Заказано;
		СтрокаТаблицы.ЕдиницаИзмерения = ВыборкаДетальныеЗаписи.ЕдиницаИзмерения;
		
	КонецЦикла;
	

	
КонецПроцедуры
