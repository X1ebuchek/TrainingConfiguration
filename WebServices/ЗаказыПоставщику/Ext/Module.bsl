
Функция ПолучитьЗаказы()
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ЗаказПоставщикуПродукты.Продукт) КАК ПродуктПредставление,
		|	ЗаказПоставщикуПродукты.Количество КАК Количество,
		|	ПРЕДСТАВЛЕНИЕ(ЗаказПоставщикуПродукты.Ссылка) КАК ПредставлениеЗаказа,
		|	ПРЕДСТАВЛЕНИЕ(ЗаказПоставщикуПродукты.Ссылка.Поставщик) КАК ПоставщикПредставление,
		|	ПРЕДСТАВЛЕНИЕ(ЗаказПоставщикуПродукты.Ссылка.Менеджер) КАК МенеджерПредставление,
		|	ЗаказПоставщикуПродукты.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПоставщику.Продукты КАК ЗаказПоставщикуПродукты
		|ИТОГИ ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Заказы = ПолучитьОбъектXDTO("Заказы");
	
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСсылка.Следующий() Цикл
		
		Заказ = ПолучитьОбъектXDTO("Заказ");
		Заказ.ПредставлениеЗаказа = ВыборкаСсылка.ПредставлениеЗаказа;
		Заказ.Поставщик = ВыборкаСсылка.ПоставщикПредставление;
		Заказ.Менеджер = ВыборкаСсылка.МенеджерПредставление;
		
		
		Продукты = ПолучитьОбъектXDTO("Продукты");
		Заказ.Продукты = Продукты;
		
		ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			СтрокаПродукты = ПолучитьОбъектXDTO("СтрокаПродукты");
			СтрокаПродукты.Продукт = ВыборкаДетальныеЗаписи.ПродуктПредставление;
			СтрокаПродукты.Количество = ВыборкаДетальныеЗаписи.Количество;
			
			Заказ.Продукты.СтрокаПродукты.Добавить(СтрокаПродукты);
			
		КонецЦикла;
		
		 
		Заказы.Заказ.Добавить(Заказ);
		
	КонецЦикла;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, Заказы);
	
	ТекстXML = ЗаписьXML.Закрыть();
	Возврат ТекстXML;
	
	
	//Возврат Заказы;	
	
КонецФункции

Функция ПолучитьОбъектXDTO(НаименованиеОбъекта)
		
	Тип = ФабрикаXDTO.Тип("http://localhost/shawarman1", НаименованиеОбъекта);
	Возврат ФабрикаXDTO.Создать(Тип);  
	
КонецФункции

